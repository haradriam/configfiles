source ~/.vimrc

" NERD Tree and Tag List
"let Tlist_Use_Right_Window = 0
let Tlist_GainFocus_On_ToggleOpen = 0
let Tlist_Close_On_Select = 0
let g:NERDTreeQuitOnOpen = 0

let s:open_sidebar = 1
if &diff
  let s:open_sidebar = 0
endif
let Tlist_Inc_Winwidth = 0

function! OpenSidebar()
  NERDTree
  TlistOpen
  wincmd J
  wincmd W
  wincmd L
  NERDTreeFocus
  normal AA
  wincmd p
endfunction

autocmd VimEnter *
      \ if (s:open_sidebar) |
      \   call OpenSidebar() |
      \ endif

" Auto quit Vim when actual files are closed
function! CheckLeftBuffers()
  if tabpagenr('$') == 1
    let i = 1
    while i <= winnr('$')
      if getbufvar(winbufnr(i), '&buftype') == 'help' ||
            \ getbufvar(winbufnr(i), '&buftype') == 'quickfix' ||
            \ exists('t:NERDTreeBufName') &&
            \   bufname(winbufnr(i)) == t:NERDTreeBufName ||
            \ bufname(winbufnr(i)) == '__Tag_List__'
        let i += 1
      else
        break
      endif
    endwhile
    if i == winnr('$') + 1
      qall
    endif
    unlet i
  endif
endfunction
autocmd BufEnter * call CheckLeftBuffers()
